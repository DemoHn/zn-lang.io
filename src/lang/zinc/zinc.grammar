@top 程序 { (标点符号 | 括号 | 运算符 | 纯数值 | 文本 | 关键字 | 标识符)* }

括号 {
  OP左方括 | OP右方括 | OP左圆括 | OP右圆括 | OP左花括 | OP右花括
}

////// 基础表达式-文本 //////

文本 { "“”" | "“"  文本内容  "”" }

文本内容 {
  (_纯文本 | 文本)*
}

//////

@skip { 空白符 | 换行符 | 行注释 | 块注释 }

@tokens {
    // 关键字列表
    关键字 {  
        "导入" | "令" | "为" | "恒为" | "再如" | "如果" | "如何" | "何为" | "不为" | "不等于" | 
        "不大于" | "不小于" | "小于" | "大于" | "以" | "否则" | "每当" | "新建" | "定义" | "其" |
        "或" | "且" | "之" | "的" | "等于" | "输入" | "输出" | "遍历" | "得到" | "抛出" | 
        "接到" | "继续循环" | "结束循环"
    }

    标点符号 { $[,，、:：;；?？!！] }

    OP左方括 { "[" | "【" } 
    OP右方括 { "]" | "】" } 
    OP左圆括 { "(" | "（" } 
    OP右圆括 { ")" | "）" } 
    OP左花括 { "{" }
    OP右花括 { "}" }

    空白符 { $[\u0009 \u000b\u00a0\u1680\u2000-\u200a\u202f\u205f\u3000\ufeff]+  }
    换行符 { $[\r\n\u2028\u2029] }

    运算符 {
      "&" | "@" | "#" | "=" | "<" | "<=" | ">" | ">=" | 
      "+" | "-" | "*" | "/" | "/=" | "|" | "%"
    }

    行注释 { ("//" ![\n]*) | ("注" $[0-9]* "：" ![“\n]*) }
    块注释 { "/*" _* "*/" }

    标识符 { _ID1 (_ID1 | $[.*/%])* }

    // workaround: 标识符排除 的、之、且、或、以、其 等单字词，防止标识符后面的关键字识别不出来
    _ID1 { $[+$^_0-9a-zA-Z\u00A0-\u2017\u201A-\u201B\u201E-\u3000\u3002-\u3009\u3012-\u4e13\u4e15-\u4E4A\u4E4C-\u4ee4\u4ee6-\u5175\u5177-\u6215\u6217-\u7683\u7685-\uFF00\uFF02-\uFF07\uFF0A-\uFF0B\uFF0D-\uFF19\uFF1C-\uFF1E\uFF20-\u{10FFFF}] | "-" }

    // Any character that is NOT a quote. This consumes the literal text
    // between quoted segments.
    _纯文本 { ![“”「」]+ }

    纯数值 { $[-+]?  $[0-9]+  ("." $[0-9]+)? ((($[eE] $[-+]) | ("*10^")) $[+-]? $[0-9]+)? }

    @precedence {
        "/*", 块注释, 行注释, "注：", 文本, 文本内容, _纯文本,
        关键字, 空白符, 换行符, 运算符, 纯数值, 标识符
    }
}

@external propSource highlighting from "./zinc-highlight"